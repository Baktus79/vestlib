plugins {
    id 'java'
    id 'maven-publish'
    id 'io.github.goooler.shadow' version '8.1.8'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.github.Baktus79'
            artifactId 'vestlib'
            version '1.0.0'
            artifact file("build/libs/VestLibs.jar")
        }
    }
}

group = 'no.vestlandetmc'
version = '1.0.0'

repositories {
    mavenCentral()
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
    maven { url 'https://oss.sonatype.org/content/repositories/public' }
    maven { url 'https://repo.aikar.co/content/groups/aikar/' }

}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT'
    implementation 'fr.mrmicky:fastboard:2.1.3'
    implementation 'co.aikar:acf-paper:0.5.1-SNAPSHOT'
    implementation 'dev.triumphteam:triumph-gui:3.1.10'
}

shadowJar {
    relocate 'dev.triumphteam.gui', 'no.vestlandetmc.vestlibs.gui'
    relocate 'fr.mrmicky.fastboard', 'no.vestlandetmc.vestlibs.fastboard'
    relocate 'co.aikar.commands', 'no.vestlandetmc.vestlibs.acf'
    relocate 'co.aikar.locales', 'no.vestlandetmc.vestlibs.locales'

    archiveFileName.set('VestLibs.jar')
}

jar.dependsOn shadowJar

jar {
    enabled = false
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}